plugins {
    id 'java'
	id 'war'
    id "eu.butter.gradle.js" version "2.15.1"
}

description = "Web Archive Viewer and Expositor"
version = '2.1.0'

ext {
    releaseDate = 'May 21 2020'
}

repositories {
    jcenter()
}

compileJava   {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

dependencies {
    compile 'javax.servlet:jstl:1.2'
    providedCompile 'javax.json:javax.json-api:1.1.4'
	providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
}

javascript.source {
    dev {
        js {
            srcDir "${projectDir}/src/main/webapp/resources/js/src"
            include "*.js"
        }
    }
}

combineJs {
    encoding = "UTF-8"
    source = javascript.source.dev.js.files
    dest = file("${buildDir}/web/resources/js/combined.js")
}

/*combineJs {
    source = ["${projectDir}/src/main/webapp/resources/js/src/file1.js", "${projectDir}/js/file2.js"]
    dest = file("${buildDir}/web/resources/js/combined.min.js")
}*/

minifyJs {
    source = "${buildDir}/web/resources/js/combined.js"
    dest = file("${buildDir}/web/resources/js/combined.min.js")
    closure.compilationLevel = 'WHITESPACE_ONLY'
    dependsOn combineJs
}

war {
    archiveName 'wave.war' // Otherwise defaults to having version attached which is pain to re-deploy on version upgrade
    filesMatching('WEB-INF/web.xml') {
        filter {
            line -> line.replaceAll("@VERSION@", version)
        }
        filter {
            line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
        filter {
            line -> line.replaceAll("@PRODUCTION_RELEASE@", "true")
        }
    }

    from("${buildDir}/web/resources/js") {
        include 'combined.min.js'
        into "resources/js"
    }

    dependsOn minifyJs
}